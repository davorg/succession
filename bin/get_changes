#!/usr/bin/perl

use strict;
use warnings;
use feature qw[say state];

use Data::Dumper;
use Dancer2::Core::Request;

use Succession::App;

my $app = Succession::App->new({ request => bless {}, 'Dancer2::Core::Request' });

my $sch = $app->model->schema;
my $ch_rs = $sch->resultset('ChangeDate');
my $pe_rs = $sch->resultset('Person');

my @changes = {
  date => $app->earliest,
  succ => [ map { $_->id } @{ $app->model->succession_on_date($app->earliest) } ],
};

while (my $cd = $ch_rs->next) {
  push @changes, {
    cd   => $cd,
    date => $cd->change_date,
    succ => [ split /:/, $cd->succession ],
  };
}

for (1 .. $#changes) {
  process_change($_);
}

sub process_change {
  $_ = shift;

  my @new = @{$changes[$_]->{succ}};
  my @old = @{$changes[$_-1]->{succ}};

  my $new_i = my $old_i = 0;

  while ($new_i <= $#new and $old_i <= $#old) {
    if ($new[$new_i] eq $old[$old_i]) {
      # No changes here.
      $old_i++; $new_i++;
      next;
    }

    my ($old_incr, $new_incr) = process_position(
      $old[$old_i], $new[$new_i], $changes[$_],
    );

    $old_i += $old_incr;
    $new_i += $new_incr;
  }
}

sub process_position {
  my ($old, $new, $change) = @_;

  my $old_p = get_person($old);
  my $new_p = get_person($new);

  if ($old_p->sovereigns->first and
    $old_p->sovereigns->first->start == $change->{date}) {
    $change->{cd}->add_to_changes({
      person_id => $old,
      description => 'became sovereign',
    });
    return (1, 0);
  }

  if ($new_p->born == $change->{date}) {
      $change->{cd}->add_to_changes({
        person_id => $new_p->id,
        description => 'was born',
      });
    return (2, 1);
  }

  if ($new eq "${old}x") {
    # Same number, but an x has been added
    $change->{cd}->add_to_changes({
      person_id => $old,
      description => 'was excluded',
    });
    return (1, 1);
  }

  if ("${new}x" eq $old) {
    # Same number, but the x has been removed
    $change->{cd}->add_to_changes({
      person_id => $new,
      description => 'was unexcluded',
    });
    return (1, 1);
  }

  if ($old_p->died->clone->add(days => 1) == $change->{date}) {
    $change->{cd}->add_to_changes({
      person_id => $old_p->id,
      description => 'died',
    });
    return (2, 1);
  }

  say 'Date: ', $change->{date};
  say 'Old: ', $old_p->born, ' - ', $old_p->died;
  say 'New: ', $new_p->born, ' - ', $new_p->died;

  say $change->{date}->ymd .
    " Weirdness: $old/$new";
  return(1, 1);
}

# say Dumper \@changes;

sub get_person {
  state $people;

  my $id = shift;

  $id =~ s/x$//;

  $people->{$id} //= $pe_rs->find($id);

  return $people->{$id};
}
