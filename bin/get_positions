#!/usr/bin/perl

use strict;
use warnings;
use feature 'say';

use DateTime::Format::Strptime;
use Succession::App;

my $app = Succession::App->new;
my $sch = $app->model->schema;

my $start;
if (@ARGV) {
  my $dtp = DateTime::Format::Strptime->new(
    pattern => '%Y-%m-%d',
    on_error => 'croak',
  );
  $start = $dtp->parse_datetime($ARGV[0]);
} else {
  $start = $app->earliest;
}

foreach ($sch->resultset('ChangeDate')->search({
    change_date => { '>=' => $start },
  }, {
    order_by => 'change_date',
  })) {
  say $_->change_date;
  $app = Succession::App->new(date => $_->change_date);
  my $succ = $app->succession;
  say scalar @$succ . ' people in the list';

  my $x = 1;
  foreach my $p (@$succ) {
    my $pos = $x;
    say $pos;
    $pos = -1 if $p->excluded_on_date($_->change_date);
    my $prev_pos = $p->position_obj_on_date($_->change_date);

    if ($prev_pos) {
      if ($prev_pos->position != $pos) {
        $prev_pos->update({ end => $_->change_date });
        $p->add_to_positions({
          position => $pos,
          start => $_->change_date,
        });
      }
    } else {
      my $start = $_->change_date;
      $start = undef if $start == $p->born;
      $p->add_to_positions({
        position => $pos,
        start => $start,
      });
    }
    ++$x if $pos != -1;
  }
}
